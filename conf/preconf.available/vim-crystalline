" vim: sts=2 sw=2 ft=vim:
"
" Basic design
" Left most:    Mode for cursor
" Left center:  File property (git/file system)
" Right center: File property (internal content type)
" Right most:   Data at cursor
"
set laststatus=2
set showtabline=2
set guioptions-=e
set noshowmode

let g:crystalline_enable_sep = 1
let g:crystalline_statusline_fn = 'StatusLine'
let g:crystalline_tabline_fn = 'TabLine'

" Color choices
" badwolf  gruvbox     molokai     shadesofpurple
" default  hybrid      onedark     solarized
" dracula  jellybeans  papercolor
let g:crystalline_theme = 'default'

"set background=light
"set background=dark

function! StatusLine(current, width)

  " left
  if a:current
    let l:s = crystalline#mode()
    let l:s .= '%{&paste ? "PASTE " : ""}'
    let l:s .= crystalline#right_mode_sep('')
  else
    let l:s = '%#CrystallineInactive#'
  endif

  " Skip git branch for HELP or small window
  if &filetype !=# 'help' && a:width >= 80
    if exists('g:loaded_gitbranch') && g:loaded_gitbranch == 1
      " https://github.com/itchyny/vim-gitbranch
      let l:b = gitbranch#name()
    elseif exists('g:loaded_fugitive') && g:loaded_fugitive == 1
      " https://github.com/tpope/vim-fugitive
      let l:b = fugitive#head()
    else
      " No way to get git branch name
      let l:b = ''
    endif
    if l:b !=# ''
      let l:b = ' áš  ' . l:b . ' '
    endif
    let l:s .= l:b
  endif
  " Skip RO for HELP
  if &filetype !=# 'help'
    "let l:s .= ' %<%F%m%r%h%w '
    let l:s .= ' %<%t%m%r%h%w '
  else
    "let l:s .= '%<%F%m%h%w '
    let l:s .= ' %<%t%m%h%w '
  endif
  " Skip right separator if Inactive
  if a:current
    let l:s .= crystalline#right_sep('', 'Fill')
  endif

  " center split
  let l:s .= '%='

  " right
  if a:current
    if a:width >= 80
      " Print lint result
      let l:s .= crystalline#left_sep('', 'Fill')
      if exists('g:loaded_ale') && g:loaded_ale == 1
	if ale#engine#IsCheckingBuffer(bufnr(''))
	  let l:s .= ' Linting ... '
	else
	  let l:counts = ale#statusline#Count(bufnr(''))
	  let l:s .= l:counts.total == 0 ? ' OK' : ''
	  let l:s .= l:counts.error + l:counts.style_error == 0 ? '' :
	    \ printf(' E: %d', l:counts.error + l:counts.style_error)
	  let l:s .= l:counts.total - l:counts.error - l:counts.style_error == 0 ? '' :
	    \ printf(' W: %d', l:counts.error + l:counts.style_error)
	endif
      endif
    endif
  endif
  " Always print filetype
  let l:s .= ' %{&filetype} '
  if a:current
    if a:width >= 80
      " Skip obvious encoding utf-8
      if &fileencoding !=# 'utf-8' && &fileencoding !=# ''
	let l:s .= '[%{&fileencoding}] '
      endif
      " Skip obvious file format unix
      if &fileformat !=# 'unix' && &fileformat !=# ''
	let l:s .= '[%{&fileformat}] '
      endif
    endif
    let l:s .= crystalline#left_mode_sep('')
    if a:width >= 80
      " print UNICODE code point
      let l:s .= ' U+%04B'
    endif
  endif
  let l:s .= ' %l/%L %c%V'
  if a:width >= 80
    let l:s .= ' %P '
  endif
  return l:s
endfunction

function! TabLine()
  let l:vimlabel = has('nvim') ?  ' NVIM ' : ' VIM '
  return crystalline#bufferline(2, len(l:vimlabel), 1) . '%=%#CrystallineTab# ' . l:vimlabel
endfunction
